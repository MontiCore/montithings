// (c) https://github.com/MontiCore/monticore
grammar MontiThings extends MontiArc,
                            ClockControl,
                            PortExtensions,
                            ConditionBasis,
                            PrePostCondition,
                            ConditionCatch,
                            SetDefinitions,
                            de.monticore.expressions.SetExpressions
{
    start MACompilationUnit;

    MTComponentType extends ComponentType =
      MTComponentModifier Name
      head:ComponentHead
      (ComponentInstance || ",")*
      body:ComponentBody
    ;

    MTComponentModifier =  ["interface"]? (["component"] | ["application"]);

    // MontiCore's SetInExpression defines this rule using "in". As a result, incoming ports
    // can no longer be parsed because they are read as "portin" instead of "port in". Until
    // MontiCore uses key("in") for this rule, we need to overwrite it.
    @Override
    SetInExpression implements Expression <190> =
      elem:Expression
      operator:key("in")
      set:Expression;

    /* ============================================================ */
    /* ================== ArcElement Insertions =================== */
    /* ============================================================ */

    MTCondition implements ArcElement = Condition;
    MTCatch implements ArcElement = ConditionCatch;

    /**
     * Store and restore component states in case of failure
     * Priority higher than 100 to prevent MontiCore from trying to
     * interpret "retain" as a component type's name.
     */
    MTRetainState implements ArcElement <110> = key("retain") key("state") ";";


    /* ============================================================ */
    /* ========================= Behavior ========================= */
    /* ============================================================ */

    Behavior implements ArcElement = "behavior" MCJavaBlock;

    /**
     * ASTIsPresentExpression can be used use a port regardless of whether it
     * has a value or to check if it has a value.
     * Hint: Directly accessing a port is allowed but means the value is
     * definitely accessed; if it doesn't exist, it will cause a run-time error
     *
     * Example: if (sensor?) actuator = sensor;
     * Example: actuator = sensor?;
     */
    IsPresentExpression implements Expression <400>
        = NameExpression "?";
}
