// (c) https://github.com/MontiCore/monticore
grammar MontiThings extends MontiArc,
                            AssumptionGuarantee,
                            IfThenElse,
                            MessageHandling,
                            ValueRanges {

    start MACompilationUnit;

    symbol scope MTComponentType extends ComponentType =
      MTComponentModifier Name
      head:ComponentHead
      (ComponentInstance || ",")*
      body:ComponentBody
    ;

    MTComponentModifier =  ["interface"]? (["component"] | ["application"]);

    /* ============================================================ */
    /* ==================== Control Statements ==================== */
    /* ============================================================ */

    /**
     * The calculation interval specifies in which 
     * interval the component is executed.
     */
    CalculationInterval implements ArcElement =
      "update" "interval" interval:NatLiteral TimeUnit";";

    enum TimeUnit = key("msec") | key("sec") | key("min");

    /* ============================================================ */
    /* ================ Requirements & Capabilities =============== */
    /* ============================================================ */

    /**
     *  RequirementStatements contain information about what physical requirements
     *  a component has. E.g Sensors/Actuators etc.
     */
    RequirementStatement implements ArcElement =
      "requires" (propertiess:Property | "{" properties:Property+ "}");

    Property = Name& ":" 
      (
        stringValue:StringLiteral | booleanValue:BooleanLiteral |
        doubleValue:SignedBasicDoubleLiteral | integerValue:SignedNatLiteral |
        longValue:SignedBasicLongLiteral
      ) ";";

    /**
     * The Value for empty data
     * Copied from MontiArc. We dont actually need a different syntax but MontiArc's AST class
     * also specifies how to print NoData as "null" in the target language. For C++, we need to
     * overwrite this to write an empty optional.
     */
    NoData implements Expression = "--";
}
