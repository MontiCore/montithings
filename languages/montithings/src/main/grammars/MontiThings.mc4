// (c) https://github.com/MontiCore/monticore
grammar MontiThings extends MontiArc,
                            ClockControl,
                            PortExtensions,
                            ConditionBasis,
                            PrePostCondition,
                            ConditionCatch,
                            SetDefinitions,
                            Behavior,
                            de.monticore.ocl.OCLExpressions,
                            de.monticore.ocl.OptionalOperators,
                            de.monticore.SIUnitTypes4Computing,
                            de.monticore.SIUnitLiterals
{
    start MACompilationUnit;

    MTComponentType extends ComponentType =
      MTComponentModifier Name
      head:ComponentHead
      (ComponentInstance || ",")*
      body:ComponentBody
    ;

    MTComponentModifier =  ["interface"]? "component";

    /* ============================================================ */
    /* ================== ArcElement Insertions =================== */
    /* ============================================================ */

    MTCondition implements ArcElement = Condition;
    MTCatch implements ArcElement = ConditionCatch;
    MTEveryBlock implements ArcElement = EveryBlock;

    /**
     * Store and restore component states in case of failure
     * Priority higher than 100 to prevent MontiCore from trying to
     * interpret "retain" as a component type's name.
     */
    MTRetainState implements ArcElement <110> = key("retain") key("state") ";";


    /* ============================================================ */
    /* ========================= Behavior ========================= */
    /* ============================================================ */

    Behavior implements ArcElement = "behavior" (Name@Port || ",")* MCJavaBlock;

    PublishPort implements MCStatement =
        "publish" publishedPorts:(Name@Port || ",")+ ";";

    /**
     * ASTIsPresentExpression can be used use a port regardless of whether it
     * has a value or to check if it has a value.
     * Hint: Directly accessing a port is allowed but means the value is
     * definitely accessed; if it doesn't exist, it will cause a run-time error
     *
     * Example: if (sensor?) actuator = sensor;
     * Example: actuator = sensor?;
     */
    IsPresentExpression implements Expression <400>
        = NameExpression "?";

    nokeyword "behavior";
}
