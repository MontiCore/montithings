/* (c) https://github.com/MontiCore/monticore */

plugins {
  id "monticore"
  id "jacoco"
  id "de.set.ecj" 
}

group = "montithings.languages"

def grammarDir = "$projectDir/src/main/grammars"
def reportsDir = "$buildDir/generated-sources/monticore/sourcecode/reports"

sourceSets {
  main {
    java.srcDirs += ["$buildDir/generated-sources/monticore/sourcecode"]
  }
  test {
    java.srcDirs += ["$buildDir/generated-test-sources/monticore/sourcecode"]
  }
  grammars {
    resources {
      srcDirs(grammarDir, reportsDir)
      include("**/*.mc4", "**/*.cd")
    }
  }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.deprecation = false
    options.warnings = false
    options.headerOutputDirectory.convention(null)
}


test {
  useJUnitPlatform()
  finalizedBy jacocoTestReport
}

configurations { grammar }

dependencies {
  grammar "de.monticore:monticore-grammar:$monticore_version:$grammars_classifier"
  grammar("montiarc.languages:montiarc-fe:$montiarc_version:$grammars_classifier"){
    exclude group:'de.monticore', module:'monticore-grammar'
  }
  grammar("de.monticore:siunits:$siunits_version:$grammars_classifier"){
    exclude group:'de.monticore', module:'monticore-grammar'
  }
  grammar("de.monticore.lang:ocl:$ocl_version:$grammars_classifier"){
    exclude group:'de.monticore', module:'monticore-grammar'
  }
  grammar files(project(":languages:monticore-extensions").projectDir.toPath().resolve("src/main/grammars"))
  grammar files(project(":languages:montiarc-extensions").projectDir.toPath().resolve("src/main/grammars"))
  grammar files(project(":languages:error-handling").projectDir.toPath().resolve("src/main/grammars"))
  grammar files(project(":languages:iot-behavior").projectDir.toPath().resolve("src/main/grammars"))

  implementation "de.monticore:monticore-grammar:$monticore_version"
  implementation "de.monticore:monticore-runtime:$monticore_version"
  //implementation "de.monticore:monticore-generator:$monticore_version"

  implementation project(":languages:montiarc-extensions")
  implementation project(":languages:monticore-extensions")
  implementation project(":languages:error-handling")
  implementation project(":languages:iot-behavior")
  implementation "de.monticore:monticore-grammar:$monticore_version"
  implementation "montiarc.languages:montiarc-fe:$montiarc_version"
  implementation "montiarc.languages:arc-fe:$montiarc_version"
  implementation "montiarc:util:$montiarc_version"
  implementation "de.monticore.lang:cd4analysis:$cd4a_version"
  implementation "de.monticore:siunits:$siunits_version"
  implementation "de.monticore.lang:ocl:$ocl_version"
  implementation "javax.json:javax.json-api:$javax_json_version"
  implementation "de.se_rwth.commons:se-commons-utilities:$se_commons_version"
  implementation "de.se_rwth.commons:se-commons-logging:$se_commons_version"
  implementation "org.antlr:antlr4-runtime:$antlr_version"
  implementation "org.apache.commons:commons-lang3:3.9"
  implementation "com.google.guava:guava:$guava_version"
  implementation "org.assertj:assertj-core:$assertj_version"
  implementation "org.jscience:jscience:$jscience_version"
  testImplementation "montiarc:util:$montiarc_version:tests"
  testImplementation "junit:junit:$junit_version"
  testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_jupiter_version"
  testImplementation "org.junit.jupiter:junit-jupiter-params:$junit_jupiter_version"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_jupiter_version"
  testImplementation "de.monticore:monticore-runtime:$monticore_version"
  testImplementation "ch.qos.logback:logback-classic:$logback_version"
}

task generateGrammarMontiThings(type: MCTask) {
  grammar = file "$grammarDir/MontiThings.mc4"
  outputDir = file "$buildDir/generated-sources/monticore/sourcecode"
  handcodedPath = [file("$projectDir/src/main/java")]
  def upToDate = incCheck("MontiThings.mc4")
  outputs.upToDateWhen { upToDate }
}

def grammarDependencies = ext {
  MontiThings = [":languages:montiarc-extensions:generateGrammarClockControl",
                 ":languages:montiarc-extensions:generateGrammarPortExtensions",
                 ":languages:error-handling:generateGrammarPrePostCondition",
                 ":languages:error-handling:generateGrammarConditionCatch",
                 ":languages:monticore-extensions:generateGrammarSetDefinitions"]
}

compileJava {
  tasks.findAll { task -> task.name.startsWith("generateGrammar") }.each {
    def grammarName = it.getName().substring("generateGrammar".length())
    it.dependsOn grammarDependencies[grammarName]
  }

  dependsOn project.collect { it.tasks.findAll { task -> task.name.startsWith("generateGrammar") } }
}

java {
  //withJavadocJar()
  withSourcesJar()
  registerFeature("grammars") {
    usingSourceSet(sourceSets.grammars)
  }
}

publishing {
  publications {
    mavenJava(MavenPublication) { from components.java }
  }
  repositories.maven {
    name = "$SE_Nexus_ID"
    credentials.username mavenUser
    credentials.password mavenPassword
    url = version.endsWith('SNAPSHOT') ? "$snapshotsRepoUrl" : "$releasesRepoUrl"
  }
}