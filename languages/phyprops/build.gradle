/* (c) https://github.com/MontiCore/monticore */

plugins {
  id "monticore"
}

group = "montithings.languages"

def grammarDir = "$projectDir/src/main/grammars"

sourceSets {
  main {
    java.srcDirs += ["$buildDir/generated-sources/monticore/sourcecode"]
  }
  test {
    java.srcDirs += ["$buildDir/generated-test-sources/monticore/sourcecode"]
  }
}

test { useJUnitPlatform() }

configurations { grammar }

dependencies {
  grammar "de.monticore:monticore-grammar:$monticore_version:$grammars_classifier"

  implementation "montiarc.languages:montiarc-fe:6.0.0-SNAPSHOT"
  implementation "montiarc.languages:montiarc-fe:6.0.0-SNAPSHOT"
  implementation "de.se_rwth.commons:se-commons-logging:1.7.9"
  implementation "com.google.guava:guava:18.0"
  implementation "org.antlr:antlr4-runtime:4.7.1"
  implementation "de.monticore:monticore-runtime:$monticore_version"
  implementation "de.monticore:monticore-grammar:$monticore_version"
  implementation "de.monticore:javaDSL:4.3.11.1"
  implementation "org.apache.commons:commons-lang3:3.9"
  testImplementation "de.monticore:monticore-runtime:$monticore_version"
  testImplementation "ch.qos.logback:logback-classic:1.1.2"
  testImplementation "org.junit.jupiter:junit-jupiter-api:5.5.2"
  testImplementation "org.junit.jupiter:junit-jupiter-params:5.5.2"
  compileOnly "de.monticore:monticore-grammar:$monticore_version"
  compileOnly "de.monticore:javaDSL:4.3.11.1"
}

task generateGrammarPhyprops(type: MCTask) {
  grammar = file "$grammarDir/Phyprops.mc4"
  outputDir = file "$buildDir/generated-sources/monticore/sourcecode"
  handcodedPath = [file("$projectDir/src/main/java")]
  def upToDate = incCheck("Phyprops.mc4")
  outputs.upToDateWhen { upToDate }
}

//TODO maven assembly plugin equivalent?

//TODO surefire?

compileJava {
  dependsOn project.collect { it.tasks.findAll { task -> task.name.startsWith("generateGrammar") } }
}

task grammars_Jar(type: Jar) {
  from "$projectDir/src/main/grammars"
  archiveClassifier = "$grammars_classifier"
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      artifactId = "$project.name"
      from components.java
      artifact grammars_Jar
    }
  }
}