/* (c) https://github.com/MontiCore/monticore */

plugins {
  id 'java-library'
  id 'monticore'  version '7.0.0' // MontiCore Plugin
}

group = "my.tagging"
version = '1.0.0-SNAPSHOT'

buildDir = file("$projectDir/target")
def mcversion  = '7.0.0'
def fdversion = '7.0.0'
def mtversion = '7.0.0-SNAPSHOT'
def se_commons_version = '7.1.1-SNAPSHOT'
def grammarName = 'Tagging'
def outDir = "$buildDir/generated-sources/"

dependencies {
  implementation "de.monticore:monticore-runtime:$mcversion"
  implementation "de.monticore:monticore-grammar:$mcversion"
  implementation "de.monticore.lang:fd-lang:$fdversion"
  implementation "de.monticore.lang:fd-analysis:$fdversion"
  implementation "montithings.languages:mtconfig:$mtversion"
  implementation "montithings.languages:montithings:$mtversion"
  implementation "commons-cli:commons-cli:1.4"
  implementation 'com.google.code.gson:gson:2.8.9'
  implementation 'com.sparkjava:spark-core:2.3'
  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
  grammar "de.monticore:monticore-grammar:$mcversion:grammars"
  implementation "de.se_rwth.commons:se-commons-utilities:$se_commons_version"
}

repositories {
  maven {
    url "https://nexus.se.rwth-aachen.de/content/groups/public"
  }
  mavenCentral()
}

// configure non-standard source sets
sourceSets {
  main.java.srcDirs += [ outDir ]
}

task generate (type: MCTask) {
  grammar = file "src/main/grammars/${grammarName}.mc4"
  outputDir = file outDir
  outputs.upToDateWhen { incCheck("${grammarName}.mc4") }
}

compileJava {
  dependsOn project.collect { it.tasks.withType(MCTask) }
}

test {
    useJUnitPlatform()
}
